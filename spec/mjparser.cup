package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected = false;
	int lastLine = 0;
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
    	lastLine = cur_token.left;
        report_error("Sintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(lastLine);
        log.error(msg.toString());
    }
    
    /*public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }*/
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, LBRACE, RBRACE, SEMI, FULL, LPAREN, RPAREN, COMMA, LBOX, RBOX, PRINT, READ, RETURN, EQUAL, VOID;
terminal String IDENT ;
terminal Boolean BOOL_CONST ;
terminal Character CHAR_CONST;
terminal Integer NUMBER_CONST;
terminal IF, ELSE, DO, WHILE, SWITCH, COLON, CASE, CONTINUE, BREAK, PLUSPLUS, MINUSMINUS;
terminal PLUS, MINUS, MUL, DIV, MOD, QUESTION, EQ, NEQ, GR, GRE, LO, LOE, ENUM;
terminal CLASS, EXTENDS, CONST, OR, AND, NEW;

nonterminal MethodDeclList, DeclList, VarDecList, ConstDeclList, ClassDeclList, ClassDecl, ConstDecl, ConstVar, ConstDeclAddition;
nonterminal FormalParamList, FormalParamDecl, FormPars, ActualPars, ActualParamList; 
nonterminal StatementList, Statement, Addop, Mulop, Relop, Assignop;
nonterminal Unmatched, Matched, OptBox, CaseList, DesignatorStatement;
nonterminal OptExpr, TypeDecl, PrintStatement, IfStatement, IfCondition;
nonterminal  And, Or, Colon, Question, Else, If, While;

nonterminal Statement2, VariableIdent, ExpressionStatement, ClassName, DoWhileStart; 

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program, ProgName, VarDecl, VarDeclAddition, MethodTypeName, MethodDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition, CondTerm, CondFact, Type, Expr, Expr1, Term, Factor, OtherDesignatorStatement;

precedence left ELSE;

Program ::= (Program) PROG ProgName:p DeclList LBRACE MethodDeclList RBRACE 
;

ProgName ::= (ProgName) IDENT:pName 
;

DeclList ::= (DeclarationList) DeclList TypeDecl
			|	
			(NoDeclarationList) /* epsilon */
			; 
			
			
TypeDecl ::= (VarTypeDeclaration) VarDecl
			|
			(ConstTypeDeclaration) ConstDecl
			|
			(ClassTypeDeclaration) ClassDecl
			;

VarDecList ::= (VarDeclarations) VarDecList VarDecl  
				|
				(NoVarDecl) /* epsilon */
				;
				
VarDecl ::= (VarDecl) Type:varType VariableIdent:varIdent OptBox VarDeclAddition SEMI ;
		
VariableIdent ::= (VariableName) IDENT:varName
				|
				(VaraibleNameError) error:e
				{: parser.report_error("Oporavak od greske pri definiciji globalne promenljive na liniji " + eleft, null);:}
				;
				
VarDeclAddition ::= (VarDeclAdd) VarDeclAddition COMMA VariableIdent OptBox
					|	
					(NoVarDeclAdd) /* epsilon */ 
					;
					
Type ::= (Type) IDENT:typeName ;


MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl
					|
					(NoMethodDecl) /* epsilon */
					;

MethodDecl ::= (MethodDeclaration) MethodTypeName LPAREN FormPars RPAREN VarDecList LBRACE StatementList RBRACE
				;

MethodTypeName ::= (MetTypeName) Type:retType IDENT:methName
					| 
					(MetVoidName) VOID:retType IDENT:methName
					;


ClassDecl ::= (ClassDeclSimple) ClassName LBRACE VarDecList RBRACE
			| 
			(ClassDeclMethods) ClassName LBRACE VarDecList LBRACE MethodDeclList RBRACE RBRACE
			| 
			(ClassDeclExtends) ClassName EXTENDS Type:parentClass LBRACE VarDecList RBRACE
			| 
			(ClassDeclExtendsMethods) ClassName EXTENDS Type:parentClass LBRACE VarDecList LBRACE MethodDeclList RBRACE RBRACE
			;
ClassName ::= (ClassName) CLASS IDENT:className;
				
ConstDecl ::= (ConstDecl) CONST Type:constType IDENT:constName EQUAL ConstVar:cVar ConstDeclAddition SEMI
			;

ConstDeclAddition ::= (ConstDeclAdd) ConstDeclAddition COMMA IDENT:constName EQUAL ConstVar:cVar
					| 
					(NoConstDeclAddition) /* epsilon */
					;
					
ConstVar ::= (NumberConst) NUMBER_CONST:numConst
			| 
			(CharConst) CHAR_CONST:charConst
			| 
			(BoolConst) BOOL_CONST:boolConst
			;


FormPars ::= (FormParams) FormalParamList 
			| 
			(NoFormParam) /* epsilon */ 
			;
		
OptBox ::= (BoxYes) LBOX RBOX
			| 
			(BoxNo) /* epsilon */
			;

FormalParamList ::= (FormalParamDecls) FormalParamList COMMA FormalParamDecl
					|
					(SingleFormalParamDecl) FormalParamDecl
					;
					
FormalParamDecl ::= (FormalParamDeclaration) Type:pt IDENT:pi OptBox
				|
				(ErrorFPClass) error:l
				{: parser.report_error("Oporavak greske kod formalnih parametara. Greska na liniji: " + lleft, null); :}
				;

StatementList ::= (Statements) StatementList Statement 
				|
				(NoStmt) /* epsilon */
				;

				  
Statement ::= (Break) BREAK SEMI
			  |
			  (Continue) CONTINUE SEMI
			  |
			  (ReturnNoExpr) RETURN SEMI
			  |
			  (ReturnExpr) RETURN Expr:t SEMI
			  |
			  (BracedStatement) LBRACE StatementList RBRACE
			  |
			  (PrintStmt)PRINT LPAREN Expr RPAREN SEMI 
			  |
			  (PrintStmtAndNumber) PRINT LPAREN Expr COMMA NUMBER_CONST RPAREN SEMI 
			  |
			  (ReadStmt) READ LPAREN Designator RPAREN SEMI 
			  |
			  (Switch) SWITCH LPAREN Expr RPAREN LBRACE CaseList RBRACE
			  |
			  (Assignment) DesignatorStatement:dest 
			  |
			  (IfStmt) IfStatement
			  |
			  (DoWhile) DoWhileStart Statement While LPAREN Condition RPAREN SEMI
			  ;
			  
DoWhileStart ::= (DoWhileStart) DO;
			  
While ::= (WhileCond) WHILE;
			  
IfStatement ::= (IfWithoutElseStatementExpression) If LPAREN IfCondition RPAREN Statement   
				|
				(IfWithElseStatementExpression) If LPAREN IfCondition RPAREN Statement Else Statement
				;
				
If ::= (If) IF;

Else ::= (Else) ELSE;
				
IfCondition ::= (IfGoodConditions) Condition
			|
			(IfErrorCondition) error:l
			{: parser.report_error("Greska pri parsiranju uslova if naredbe, oporavak od greske na liniji " + lleft, null); :}
			;
			  
CaseList ::= (Case) CaseList CASE NUMBER_CONST COLON StatementList
			|
			(EmptyCase) /* epsilon */
			;
			  
DesignatorStatement ::= (DesignatorExpression) ExpressionStatement 
						|
						(DesignatorOther) OtherDesignatorStatement SEMI
						;
						
OtherDesignatorStatement ::= (DesignatorFuncCall) Designator LPAREN ActualPars RPAREN
							|
							(DesignatorIncrement) Designator PLUSPLUS
							|
							(DesignatorDecrement) Designator MINUSMINUS
							;
						
ExpressionStatement ::= (DesignatorAssignExpression) Designator Assignop Expr SEMI
					|
					(DesignatorError) error:err SEMI
					{: parser.report_error("Oporavak od greske kod iskaza dodele na liniji " + errleft, null); :}
					;
			  
			  
Condition ::= (ConditionTerm) CondTerm
			|
			(ConditionOr) Condition Or CondTerm
			;
			
Or ::=(Or) OR;

CondTerm ::= (ConditionFact) CondFact
			|
			(ConditionAnd) CondTerm And CondFact
			;
			
And ::=(And) AND;
			
CondFact ::= (CondFactExpr) Expr1:expr
			|
			(CondFactRelop)Expr1:expr1 Relop Expr1:expr2
			;

Expr1 ::= (AddExpr) Expr1:te Addop Term:t
		 |
		 (TermExpr) Term:t
		 |
		 (NegTermExpr) MINUS Term:t
		 ;
		 
Expr ::= (ConditionExpr) Expr1:cond Question Expr1:expr1 Colon Expr1:expr2
		|
		(Expresion) Expr1:expr
		;
		
Question ::= (Question) QUESTION;

Colon ::= (Colon) COLON;

Term ::= (FactorTerm) Factor:t 
		|
		(MullTerm) Term:tt Mulop Factor:t
		;
		

Factor ::= (Const) ConstVar:c
			|
		   	(Var) Designator:d
		   	|
		   	(FuncCall) Designator:func LPAREN ActualPars RPAREN
		   	|
		   	(NewVar) NEW Type LBOX Expr RBOX
		   	|
		   	(FactorExpr) LPAREN Expr RPAREN
		   ;
		   
OptExpr ::= (YesOptExpr) LBOX Expr RBOX
		|
		(NoOptExpr) /* epsilon */
		;

ActualPars ::= (Actuals) ActualParamList 
				| 
			   (NoActuals) /* epsilon */ 
			   ;
			   
ActualParamList ::= (ActualParams) ActualParamList COMMA Expr
					|
					(ActualParam) Expr
					;

Designator ::= (DesignatorIdent) IDENT:name
			| 
			(DesignatorIdentList) Designator:des FULL IDENT:subName
			|
			(DesignatorExprList) Designator:des LBOX Expr:expr RBOX
			;

Assignop ::= (Assign) EQUAL;

Relop ::= (Equal) EQ
		|
		(NotEqual) NEQ
		|
		(Greater) GR
		|
		(GreaterOrEqual) GRE
		|
		(Lower) LO
		|
		(LowerOrEqual) LOE
		;

Addop ::= (Addop) PLUS 
		|
		(Subop) MINUS
		;
		
Mulop ::= (Mul) MUL
		|
		(Div) DIV
		|
		(Mod) MOD
		;
		
